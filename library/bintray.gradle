apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

group = GROUP_ID // Maven Group ID for the artifact
version = VERSION_NAME

install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'aar'
                groupId GROUP_ID
                artifactId ARTIFACT_ID

                // Add your description here
                name LIB_NAME
                description LIB_DESC
                url LIB_URL

                // Set your license
                licenses {
                    license {
                        name LICENSE_NAME
                        url LICENSE_URL
                    }
                }
                developers {
                    developer {
                        id DEVELOPER_ID
                        name DEVELOPER_NAME
                        email DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection LIB_GIT_URL
                    developerConnection LIB_GIT_URL
                    url LIB_URL
                }
            }
        }
    }
}



if (project.hasProperty("android")) { // Android libraries
    task sourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.srcDirs
    }

    task javadoc(type: Javadoc) {
        source = android.sourceSets.main.java.srcDirs
        classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    }
} else { // Java libraries
    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    //archives javadocJar
    archives sourcesJar
}

// Bintray
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties.getProperty("bintray.user") //add bintrayUser property to local.properties
    // in root project
    key = properties.getProperty("bintray.apikey") //add bintrayApiKey property to local
    // .properties in root project

    configurations = ['archives']
    pkg {
        repo = 'android'
        name = LIB_NAME
        desc = LIB_DES
        websiteUrl = LIB_URL
        vcsUrl = LIB_GIT_URL
        licenses = ['Apache-2.0']
        userOrg = USER_ORG
        publish = true
        labels = ['aar', 'android', 'quintype']
        publicDownloadNumbers = true
        version {
            desc = LIB_DESC
        }
    }
}